SET QUOTED_IDENTIFIER, ANSI_NULLS ON
GO
CREATE PROCEDURE [dbo].[CATEGORY_Create] 
	@p_CATEGORY_DATA_JSON NVARCHAR(MAX)
AS
BEGIN
	-- SET NOCOUNT ON added to prevent extra result sets from
	-- interfering with SELECT statements.
	SET NOCOUNT ON;
	BEGIN TRANSACTION
	BEGIN TRY
		DECLARE	 @p_ADMIN_ID NVARCHAR(20),
				 @p_CATEGORY_NAME NVARCHAR(50),
				 @p_CATEGORY_IMAGE NVARCHAR(MAX),
				 @p_CATEGORY_STATUS NVARCHAR(20)

		SELECT	 @p_ADMIN_ID = ADMIN_ID,
				 @p_CATEGORY_NAME = CategoryName,
				 @p_CATEGORY_IMAGE = CategoryImage,
				 @p_CATEGORY_STATUS = CategoryStatus
		FROM OPENJSON(@p_CATEGORY_DATA_JSON)
		WITH(
			ADMIN_ID NVARCHAR(20) '$.ADMIN_ID',
			CategoryName NVARCHAR(50) '$.CATEGORY_NAME',
			CategoryImage NVARCHAR(MAX) '$.CATEGORY_IMAGE',
			CategoryStatus NVARCHAR(10) '$.CATEGORY_STATUS'
		)

		declare @p_ROLE_RESULT nvarchar(10)
		exec [dbo].[CHECK_ROLE] @p_ADMIN_ID = @p_ADMIN_ID, @p_RESULT = @p_ROLE_RESULT output

		IF @p_ROLE_RESULT <> 1
		begin
			rollback transaction
			select N'Không đủ quyền' as RESULT,
					403 as CODE
			return
		end

		IF @p_CATEGORY_NAME = '' OR @p_CATEGORY_NAME IS NULL 
		BEGIN
            ROLLBACK TRANSACTION
			select N'Dữ liệu trống' as RESULT,
					422 as CODE
			RETURN
		END

		IF EXISTS (SELECT 1 FROM CATEGORIES WHERE CATEGORY_NAME = @p_CATEGORY_NAME)
		BEGIN
			ROLLBACK TRANSACTION
			select N'Danh mục đã tồn tại' as RESULT,
					409 as CODE
			RETURN
		END

		INSERT INTO CATEGORIES (CATEGORY_NAME, CATEGORY_IMAGE, CATEGORY_STATUS)
		VALUES (@p_CATEGORY_NAME, @p_CATEGORY_IMAGE, @p_CATEGORY_STATUS)

		IF @@ROWCOUNT <> 0
		BEGIN
			COMMIT TRANSACTION
			SELECT CATEGORY_ID, CATEGORY_NAME, CATEGORY_IMAGE, CATEGORY_STATUS, N'Tạo thương hiệu thành công' as RESULT, 201 as CODE
			FROM CATEGORIES
			WHERE CATEGORY_NAME = @p_CATEGORY_NAME
		END
	END TRY
	BEGIN CATCH
		ROLLBACK TRANSACTION
		select ERROR_MESSAGE() as RESULT,
				ERROR_NUMBER() as CODE
		RETURN
	END CATCH
END
GO