SET QUOTED_IDENTIFIER, ANSI_NULLS ON
GO
CREATE PROCEDURE [dbo].[PRODUCT_Search]
	@p_PRODUCT_DATA_JSON NVARCHAR(MAX),
	@p_TOTAL_RECORD INT OUTPUT

AS
BEGIN
	-- SET NOCOUNT ON added to prevent extra result sets from
	-- interfering with SELECT statements.
	SET NOCOUNT ON;
	DECLARE	 @p_PAGE_NUMBER INT,
			 @p_PAGE_SIZE INT,
			 @p_PRODUCT_NAME NVARCHAR(MAX)

	SELECT	 @p_PAGE_NUMBER = PageNumber,
			 @p_PAGE_SIZE = PageSize,
			 @p_PRODUCT_NAME = ProductName
	FROM OPENJSON(@p_PRODUCT_DATA_JSON)
	WITH(
		PageNumber INT '$.PAGE_NUMBER',
		PageSize INT '$.PAGE_SIZE',
		ProductName NVARCHAR(MAX) '$.PRODUCT_NAME'
	)

	SET @p_PAGE_NUMBER = ISNULL(@p_PAGE_NUMBER, 1)
	SET @p_PAGE_SIZE = ISNULL(@p_PAGE_SIZE, 10)
	SET @p_PRODUCT_NAME = ISNULL(@p_PRODUCT_NAME, '')

	IF @p_PAGE_NUMBER < 1 SET @p_PAGE_NUMBER = 1
	IF @p_PAGE_SIZE < 1 SET @p_PAGE_SIZE = 10

	SELECT @p_TOTAL_RECORD = COUNT(*) FROM PRODUCTS

	SELECT P.PRODUCT_NAME, P_I.IMAGE_NAME, P.PRODUCT_PRICE, P.PRODUCT_STATUS
	FROM PRODUCTS P
	INNER JOIN PRODUCT_IMAGE P_I ON P_I.PRODUCT_ID = P.PRODUCT_ID
	WHERE P.PRODUCT_NAME LIKE N'%' + @p_PRODUCT_NAME +  N'%'
	ORDER BY P.PRODUCT_ID
	OFFSET (@p_PAGE_NUMBER - 1) * @p_PAGE_SIZE ROWS
	FETCH NEXT @p_PAGE_SIZE ROWS ONLY
END
GO