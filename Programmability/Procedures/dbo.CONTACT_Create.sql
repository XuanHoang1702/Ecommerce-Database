SET QUOTED_IDENTIFIER, ANSI_NULLS ON
GO
CREATE PROCEDURE [dbo].[CONTACT_Create]
	@p_CONTACT_DATA_JSON NVARCHAR(MAX)
AS
BEGIN
	-- SET NOCOUNT ON added to prevent extra result sets from
	-- interfering with SELECT statements.
	SET NOCOUNT ON;
	BEGIN TRANSACTION
	BEGIN TRY
		DECLARE	 @p_USER_EMAIL NVARCHAR(255),
				 @p_USER_NAME NVARCHAR(255),
				 @p_TITLE NVARCHAR(255),
				 @p_CONTENT NVARCHAR(MAX)

		SELECT  @p_USER_EMAIL = UserEmail,
				@p_TITLE = Title,
				@p_CONTENT = Content
		FROM OPENJSON(@p_CONTACT_DATA_JSON)
		WITH(
			UserEmail NVARCHAR(255) '$.USER_EMAIL',
			Title NVARCHAR(255) '$.TITLE',
			Content NVARCHAR(MAX) '$.CONTENT'
		)

		IF NOT EXISTS (SELECT 1 FROM [USERS] WHERE [USER_EMAIL] = @p_USER_EMAIL)
		BEGIN
			ROLLBACK TRANSACTION
			select N'Tài khoản không tồn tại' as RESULT,
					422 as CODE
			RETURN
		END

		SELECT @p_USER_NAME = USER_FIRST_NAME
		FROM [USERS]
		WHERE [USER_EMAIL] = @p_USER_EMAIL

		INSERT INTO CONTACT (USER_EMAIL, [USER_NAME], TITLE, CONTENT, CONTACT_STATUS, CREATED_AT)
		VALUES (@p_USER_EMAIL, @p_USER_NAME, @p_TITLE, @p_CONTENT, 'PENDING', GETDATE())

		IF @@ROWCOUNT = 1
		BEGIN
			COMMIT TRANSACTION
			SELECT  @p_USER_EMAIL AS USER_EMAIL,
					@p_USER_NAME AS [USER_NAME],
					@p_TITLE AS TITLE,
					@p_CONTENT AS CONTENT,
					'PENDING' AS CONTACT_STATUS,
					GETDATE() AS CREATED_AT,
					N'Gửi yêu cầu thành công' as RESULT,
					201 as CODE
		END
	END TRY
	BEGIN CATCH
		ROLLBACK TRANSACTION
		select ERROR_MESSAGE() as RESULT,
				ERROR_NUMBER() as CODE
		RETURN
	END CATCH
END
GO