SET QUOTED_IDENTIFIER, ANSI_NULLS ON
GO
CREATE PROCEDURE [dbo].[DISCOUNT_List]
	@p_PAGE_NUMBER INT,
	@p_PAGE_SIZE INT,
	@p_TOTAL_RECORD INT OUTPUT
AS
BEGIN
	SET NOCOUNT ON;

	IF @p_PAGE_NUMBER < 1 OR @p_PAGE_NUMBER IS NULL
	BEGIN
		SET @p_PAGE_NUMBER = 1
	END

	IF @p_PAGE_SIZE < 1 OR @p_PAGE_SIZE IS NULL
	BEGIN
		SET @p_PAGE_SIZE = 5
	END

	SELECT @p_TOTAL_RECORD = COUNT(*) 
	FROM DISCOUNT

	SELECT  
		P.PRODUCT_NAME, 
		P.PRODUCT_PRICE, 
		D.DISCOUNT_PERCENT, 
		D.START_DT, 
		D.END_DT,
		P_I.IMAGE_NAME
	FROM DISCOUNT D
	INNER JOIN PRODUCTS P ON P.PRODUCT_ID = D.PRODUCT_ID
	LEFT JOIN (
		SELECT PRODUCT_ID, IMAGE_NAME
		FROM PRODUCT_IMAGE
		WHERE IMAGE_ID IN (
			SELECT MIN(IMAGE_ID)
			FROM PRODUCT_IMAGE
			GROUP BY PRODUCT_ID
		)
	) P_I ON P.PRODUCT_ID = P_I.PRODUCT_ID
	ORDER BY P.PRODUCT_ID
	OFFSET (@p_PAGE_NUMBER - 1) * @p_PAGE_SIZE ROWS
	FETCH NEXT @p_PAGE_SIZE ROWS ONLY
END
GO